// NPC Script for handling mounts like Dragon, Madogear, and Wolf.

izlude,88,148,5	script	Mount Manager	-1,{	
	callsub OnClick;
	end;

	OnInit:
		callfunc("customizeNPCSprite", 20388, 0, 0, 0, 0, 0, 0, 0, 53, 16);
		end;

	OnClick:
		// Initialize variables
		set .@NPC_NAME$, "^a39f00[ Litzy ]^000000";
		set .@DRAGON_COLORTXT$, "^388E3CDragon^000000"; set .@MADOGEAR_COLORTXT$, "^ff6800Madogear^000000"; set .@WOLF_COLORTXT$, "^0288D1Wolf^000000";
		set .@KNIGHT_ID, roclass(EAJ_KNIGHT); set .@BLACKSMITH_ID, roclass(EAJ_BLACKSMITH); set .@HUNTER_ID, roclass(EAJ_HUNTER);
		setarray .@BASELOOK_KNIGHT[0], 4060, 4252; 		setarray .@BASELOOK_KNIGHT$[0], "Rune Knight", "Dragon Knight";  
		setarray .@BASELOOK_BLACKSMITH[0], 4064, 4253; 	setarray .@BASELOOK_BLACKSMITH$[0], "Mechanic", "Meister";  
		setarray .@BASELOOK_HUNTER[0], 4062, 4257; 		setarray .@BASELOOK_HUNTER$[0], "Ranger", "Windhawk";  
		set .@baselook, baselook;

		// Start of dialogue
		cutin "litzy", 2;
		mes .@NPC_NAME$;
		mes "Hey there~!";
		mes "I'm Litzy, should you need guidance for";
		mes .@DRAGON_COLORTXT$ + ", " +.@MADOGEAR_COLORTXT$+ ", and " +.@WOLF_COLORTXT$+ " mounts,";
		mes "then I'm here to assist you.";
		next;
		set .@mount_select, select("Can you help me train a " +.@DRAGON_COLORTXT$+ "?:Can you build a "+.@MADOGEAR_COLORTXT$+"?:Can you call a lone " +.@WOLF_COLORTXT$+ "?:Cancel");
		switch(.@mount_select){
			// Check Halter lead first.
			if (ismounting() && !getskilllv("NC_MADOLICENCE")){
					emotion ET_WRAP;
					mes .@NPC_NAME$;
					mes "Please dismount from your ^000fff'Halter Lead'^000000 before acquiring a new mount here.";
					close3;
				}
			
			case 1: //Dragon Mounts, rk_version 1 = Default, 2 = Legacy, 3 = Alternate (jRO)
				if (!countinarray(.@BASELOOK_KNIGHT, .@baselook) && !rk_version) {// Knight, LK not found
					callfunc("check_job_costume", .@NPC_NAME$, .@BASELOOK_KNIGHT$[0], .@BASELOOK_KNIGHT$[1], .@DRAGON_COLORTXT$);
				}

				set .@dragon_color, 1;
				if (getskilllv("KN_CAVALIERMASTERY") != 5){
					mes .@NPC_NAME$;
					mes "I'm sorry, you need to have";
					mes "<URL>Level 5 Cavalry Masyery<INFO>https://irowiki.org/classic/Cavalier_Mastery</INFO></URL>";
					emotion ET_SORRY;
					close3;
				}

				// Intro speech for special dragons
				if (rk_version == 2){
					set .@dragon_color, callfunc("handle_rk_legacy_mount", .@NPC_NAME$, .@DRAGON_COLORTXT$, .@KNIGHT_ID);
				}

				// RK Version not legacy
				if ((checkdragon() && rk_version != 2) || (checkriding() && rk_version == 3)){
					emotion ET_OK;
					mes .@NPC_NAME$;
					mes "You already have a " +.@DRAGON_COLORTXT$+ ".";
					mes "Remember to give it head pats regularly~";
					close3;
				}

				// First time getting a mount after Job Costume change
				if (!getskilllv("RK_DRAGONTRAINING")) {
					mes .@NPC_NAME$;
					mes "Seems like you haven't learned";
					mes "'^388E3CDragon Training^000000' yet.";
					mes "Let me show you...";
					mes "<URL>How to train a dragon~<INFO>https://www.youtube.com/watch?v=ZDyEERuK31Y</INFO></URL>";
					next;

					progressbar "ffff00", 3;
					specialeffect 670, SELF;
					soundeffect "dragonfear.wav", 0;
					next;

					mes "You learned 'Dragon Training'.";
					mes "You got a " +.@DRAGON_COLORTXT$+ ".";
					skill "RK_DRAGONTRAINING", 5, SKILL_PERM_GRANT;
					next;

					specialeffect2 1218, SELF;
					soundeffect "dragon_h.wav", 0;
					callfunc("exit_speech_litzy", .@NPC_NAME$, .@KNIGHT_ID, .@DRAGON_COLORTXT$);

				} else if (getskilllv("RK_DRAGONTRAINING")){ // Just remount
					specialeffect2 1803, SELF;
					soundeffect "dragon_h.wav", 0;
					callfunc("exit_speech_litzy", .@NPC_NAME$, .@KNIGHT_ID, .@DRAGON_COLORTXT$);
				}

				// For rk_version 3, use pecopeco only
				if (rk_version == 3) {
					setriding 1;
					close3;
				}
				// Finally, apply dragon mount here
				setdragon .@dragon_color;
				close3;

			case 2: //Madogear Mounts
				if (!countinarray(.@BASELOOK_BLACKSMITH, .@baselook)) {// BS, WS not found
					callfunc("check_job_costume", .@NPC_NAME$, .@BASELOOK_BLACKSMITH$[0], .@BASELOOK_BLACKSMITH$[1], .@MADOGEAR_COLORTXT$);
				}
				
				if (!getskilllv("NC_MADOLICENCE")) {
					mes .@NPC_NAME$;
					mes "Seems like you haven't gotten";
					mes "'^388E3CMadogear License^000000' yet.";
					mes "Let me show you...";
					mes "<URL>How to build a Madogear~<INFO>https://www.youtube.com/watch?v=h9nB5ZzbSO8</INFO></URL>";
					next;

					progressbar "ffff00", 3;
					specialeffect 1854, SELF;
					next;

					mes "You got 'Madogear License'.";
					mes "You got a " +.@MADOGEAR_COLORTXT$+ ".";
					skill "NC_MADOLICENCE", 5, SKILL_PERM_GRANT;
					next;
				}

				if (ismounting()) {
					mes .@NPC_NAME$;
					mes "Looks like you¡¯ve got your "+.@MADOGEAR_COLORTXT$+" all set up!";
					next;
					callfunc("exit_speech_litzy", .@NPC_NAME$, .@BLACKSMITH_ID, .@MADOGEAR_COLORTXT$);
				} else {
					setmounting();
					specialeffect2 1835, SELF;
					sc_start SC_SPEEDUP0, 180000, 25;
					soundeffect "CD_FRAMEN.wav", 0;
					callfunc("exit_speech_litzy", .@NPC_NAME$, .@BLACKSMITH_ID, .@MADOGEAR_COLORTXT$);
				}

				// Give special mount item
				if(countitem(68000) < 1) {
					next;
					mes .@NPC_NAME$;
					mes "Take this exclusive item";
					mes "to mount or unmount anytime.";
					mes "Don't you lose it~!";
					getnameditem 68000, getcharid(0);
				}
				close3;

			case 3: // Wolf Mounts
				if (!countinarray(.@BASELOOK_HUNTER, .@baselook)){
					callfunc("check_job_costume", .@NPC_NAME$, .@BASELOOK_HUNTER$[0], .@BASELOOK_HUNTER$[1], .@WOLF_COLORTXT$);
				}

				if (checkoption(Option_Wugrider)){
					mes .@NPC_NAME$;
					mes "I can tell that you're enjoying your time";
					mes "with your "+ .@WOLF_COLORTXT$ +".";
					next;
					callfunc("exit_speech_litzy", .@NPC_NAME$, .@HUNTER_ID, .@WOLF_COLORTXT$);
				}

				if (!getskilllv("RA_WUGMASTERY")) {
					mes .@NPC_NAME$;
					mes "Seems like you've yet to learn";
					mes "'^388E3CWarg Mastery^000000'.";
					mes "Let me show you...";
					mes "<URL>How to call a lone Wolf~<INFO>https://youtu.be/Telc8mgZ0ag?si=s4iO0MtABI1EDDmy&t=16</INFO></URL>";
					next;

					progressbar "ffff00", 3;
					specialeffect 2371, SELF;
					next;

					mes "You learned 'Warg Mastery'.";
					mes "You learned 'Warg Rider'.";
					mes "You got a " +.@WOLF_COLORTXT$+ ".";
					skill "RA_WUGMASTERY", 1, SKILL_PERM_GRANT;
					skill "RA_WUGRIDER", 3, SKILL_PERM_GRANT;
					next; 

					setoption Option_Wug;
					specialeffect2 2338, SELF;
					soundeffect "wolf_die.wav", 0;
					callfunc("exit_speech_litzy", .@NPC_NAME$, .@HUNTER_ID, .@WOLF_COLORTXT$);
				} else if (getskilllv("RA_WUGMASTERY") && !checkoption(Option_Wugrider)) {
					mes .@NPC_NAME$;
					mes "Use the skill ^0063f8Warg Mastery^000000";
					mes "to call or release your "+ .@WOLF_COLORTXT$ +".";
					mes "Then, you may use ^0063f8Warg Rider^000000";
					mes "to get on your "+ .@WOLF_COLORTXT$ +".";
				}

				// Give special mount item
				if(countitem(12848) < 1 || countitem(6124) < 1) {
					next;
					mes .@NPC_NAME$;
					mes "Here are some special gifts for you";
					mes "to summon your wolf and falcon";
					mes "whenever necessary.";
					mes "Take care of them~!";
					if(countitem(12848) < 1) getnameditem 12848, getcharid(0);
					if(countitem(6124) < 1) getnameditem 6124, getcharid(0);
				}
				close3;

			default: 
				close3;
		}
}

function	script	check_job_costume	{
	set .@npc_name$, getarg(0); set .@job3_id$, getarg(1); set .@job4_id$, getarg(2); set .@mount_name$, getarg(3);

	emotion ET_STARE_ABOUT;
	mes .@npc_name$;
	mes "You should be a ^F57C00"+ .@job3_id$ +"^000000 or";
	mes "^F44336"+ .@job4_id$ +"^000000 to get a "+ .@mount_name$ +".";
	mes "To change Job Costumes, please find";
	mes "<NAVI>Celestia<INFO>izlude,88,128,0,000,0</INFO></NAVI>";
	close3;
}

function	script	handle_rk_legacy_mount	{
	set .@npc_name$, getarg(0); set .@mount_name$, getarg(1); set .@basejob_id, getarg(2);
	// Check if dragon is present
	if (!checkdragon()){
		emotion ET_BIGTHROB;
		mes .@npc_name$;
		mes "Oh!, the version of your costume";
		mes "allows you to choose a";
		mes "^8F00FFMystically Colored Dragon^000000~";
		next;
	} else {
		mes .@npc_name$;
		mes "Do you want change your "+ .@mount_name$ +"?";
		next;
		switch(select("Yes:No")){
			case 1:
				setdragon 0;
				break;
			case 2:
				callfunc("exit_speech_litzy", .@npc_name$, .@basejob_id, .@mount_name$);
				close3;
		}
	}

	// RK Legacy: Choose color of your dragon
	mes .@npc_name$;
	mes "What color do you want for your";
	mes .@mount_name$+ "?";
	next;
	switch(select("^388E3CGreen (Default)^000000:^4E342EBrown^000000:^455A64Gray^000000:^0288D1Blue^000000:^D32F2FRed^000000")){
		case 1:
			return 1; //Default Color
		case 2:
			return 2;
		case 3:
			return 3;
		case 4:
			return 4;
		case 5:
			return 5;
	}
}

// Exit speech does not end or close the NPC
function	script	exit_speech_litzy	{
	set .@npc_name$, getarg(0); set .@basejob_id, getarg(1); set .@mount_name$, getarg(2);

	switch(.@basejob_id){
		mes .@npc_name$;
		case 7: //Knight Class
			emotion ET_GOODBOY;
			mes "Keep your "+ .@mount_name$ +" nourished and happy, and it will illuminate your path, even in the darkest dungeons~ ^_^";
			break;
			
		case 10: //Blacksmith Class
			emotion ET_SPARK;
			mes "This "+.@mount_name$+" is more ";
			mes "than a machine;";
			mes "it's your steadfast companion. ";
			mes "Together, you can explore anything~ ^_^";
			break;

		case 11: //Hunter Class
			emotion ET_BEST;
			mes "With your "+.@mount_name$+" by your side,";
			mes "may you find both wisdom and wealth";
			mes "on your path.";
			mes "Like the cunning and wise wolf, Holo~ ^_^";
			break;
	}
	return;
}