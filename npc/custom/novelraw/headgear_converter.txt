izlude,94,125,3	script	Freyja#1	-1,{
	callsub OnClick;
	end;

	OnInit:
		callfunc("customizeNPCSprite", JOB_TROUVERE, SEX_FEMALE, 6, 27, 1980, 0, 1996, 5);
		end;

	OnClick:
		// Prevent user to change items while interacting to the NPC to prevent exploit
		disable_items;
		mes "[Freyja]";
		mes "Such beauty isn't it?";
		mes "It's like a reflection of the beauty that resides within us all.";
		next;
		mes "[Freyja]";
		mes "I possess the ability to transform your headgears into enchanting costumes or restore them to their original forms.";
		mes "";
		mes "Intriguing, isn't it?";
		mes "So, what will it be?";
		next;
		
		switch(select("Convert Headgear to Costume:Restore Costume to Headgear")) {
			case 1:
				set .@endingMes, callfunc("handleHeadgear", EQI_HEAD_TOP, EQI_HEAD_MID, EQI_HEAD_LOW, 0); // 0 for conversion
				break;
			case 2:
				set .@endingMes, callfunc("handleHeadgear", EQI_COSTUME_HEAD_TOP, EQI_COSTUME_HEAD_MID, EQI_COSTUME_HEAD_LOW, 1); // 1 for restoration
				break;
		}
		
		mes "[Freyja]";
		switch(.@endingMes){
			case 0:
				mes "Done, enjoy your costume headgear.";
				break;
			
			case 1:
				mes "Done, your costume has been restored to its original headgear form.";
				break;
			
			case 2:
				mes "Need some time to think about it?";
				mes "Alright, I understand.";
				break;
			
			case 3:
				mes "Oh dear,";
				mes "You must wear a ^000dddHeadgear^000000 for me to convert.";
				break;
			
			case 4:
				mes "Oh dear,";
				mes "You must wear a ^6000ddCostume-converted Headgear^000000 for me to convert.";
				break;

		}
		close;

}

// Define a new function to handle both conversion and restoration
function	script	handleHeadgear	{
	.@eqi_head_top = getarg(0);
	.@eqi_head_mid = getarg(1);
	.@eqi_head_low = getarg(2);
	.@operation = getarg(3); // 0 for conversion, 1 for restoration
	
	// Checking the equipment in each headgear position
	.@headgear_count = 0;
	if ((.@head_top_item = getequipid(.@eqi_head_top)) != -1) {
		setarray .@headgear_id[.@headgear_count], .@head_top_item;
		setarray .@headgear_pos[.@headgear_count], .@eqi_head_top;
		.@headgear_count++;
	}
	if ((.@head_mid_item = getequipid(.@eqi_head_mid)) != -1) {
		setarray .@headgear_id[.@headgear_count], .@head_mid_item;
		setarray .@headgear_pos[.@headgear_count], .@eqi_head_mid;
		.@headgear_count++;
	}
	if ((.@head_low_item = getequipid(.@eqi_head_low)) != -1) {
		setarray .@headgear_id[.@headgear_count], .@head_low_item;
		setarray .@headgear_pos[.@headgear_count], .@eqi_head_low;
		.@headgear_count++;
	}

	//End if there is no headgear equipped
	if (.@headgear_count == 0 && .@operation == 0)
		return 3;
	else if (.@headgear_count == 0 && .@operation == 1)
		return 4;

	mes "[Freyja]";
	mes "Please select what to convert.";
	mes "";
	mes "Remember, ^dd0000Random options^000000, ^00cfddCards^000000, and ^62dd00Refine Rate^000000 will be removed and ^2200ddwill not be restored^000000.";
	// Build the select options string
	.@options$ = "";
	for (.@i = 0; .@i < .@headgear_count; .@i++) {
		// Determine position string
		if (.@headgear_pos[.@i] == .@eqi_head_top) {
			.@pos$ = "Upper";
			.@posColor$ = "^dd0000";
		}
		if (.@headgear_pos[.@i] == .@eqi_head_mid) {
			.@pos$ = "Middle";
			.@posColor$ = "^01a948";
		}
		if (.@headgear_pos[.@i] == .@eqi_head_low) {
			.@pos$ = "Lower";
			.@posColor$ = "^002bff";
		}
		
		.@options$ += .@pos$ + " - [ " + .@posColor$ + getitemname(.@headgear_id[.@i]) + "^000000 ]:";
	}
	.@options$ += "Cancel";

	// Allow the player to select which headgear to handle
	next;
	.@selection = select(.@options$);
	if (.@selection <= .@headgear_count) {
		if (.@operation == 0) {
			costume .@headgear_pos[.@selection - 1];  // Convert the selected headgear to a costume
			return 0;
		}
		else {
			// Restore headgear by recreating
			dispbottom .@headgear_id[.@selection - 1];
			delitem .@headgear_id[.@selection - 1], 1;
			getitem .@headgear_id[.@selection - 1], 1;
			return 1;
		}
	}
	//If Cancel was selected
	return 2;
}